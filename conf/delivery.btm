

## Byteman script to measure delivery latency from reception of a message to its delivery
## (*excluding* the delivery time spent in the application)


HELPER org.perf.DeliveryHelper

## Channel created - needed to register DeliveryHelper as ProbeHandler
RULE ChannelCreated
CLASS org.jgroups.JChannel
METHOD init
AT EXIT
IF TRUE
    DO channelCreated($0);
ENDRULE

## Message received by the transport; associate time (in micros) with current thread
RULE MessageReceived
CLASS org.jgroups.protocols.TP
METHOD receive
AT ENTRY
IF TRUE
    DO recordReceiveTime();
ENDRULE

## Message deserialized; add PerfHeader with previously recorded time to the message
RULE MessageDeserialized
CLASS org.jgroups.protocols.TP
METHOD handleSingleMessage
AFTER INVOKE org.jgroups.Message.readFrom
IF TRUE
    DO messageDeserialized($msg);
ENDRULE


## Message batch received; add PerfHeader with current time to each message in the batch
RULE BatchReceived
CLASS org.jgroups.protocols.TP
METHOD handleMessageBatch
AFTER WRITE $batches
IF TRUE
    DO batchesReceived($batches);
ENDRULE


################ InfinispanCache ################

## Handles single messages or messages in a batch
RULE IspnMessageDelivery
CLASS org.jgroups.blocks.RequestCorrelator
METHOD dispatch
AT ENTRY
IF TRUE
    DO beforeMessageDelivery($1);
ENDRULE

RULE IspnBatchDelivery
CLASS org.jgroups.blocks.RequestCorrelator
METHOD receiveMessageBatch
AT ENTRY
IF TRUE
    DO beforeBatchDelivery($1);
ENDRULE

################ TriCache ################
## TriCache currently only implements receive(Message), once receive(MessageBatch), is implemented, the BatchDelivery
## rule needs to be changed

## Message to be delivered to application
RULE TriMessageDelivery
CLASS org.cache.impl.tri.TriCache
METHOD receive(org.jgroups.Message)
AT ENTRY
IF TRUE
    DO beforeMessageDelivery($1);
ENDRULE


## Message batch to be delivered to application
RULE TriBatchDelivery
CLASS org.cache.impl.tri.TriCache
METHOD receive(org.jgroups.util.MessageBatch)
AT ENTRY
IF TRUE
     DO beforeBatchDelivery($1);
ENDRULE


#RULE AfterBatchDelivery
#CLASS org.cache.impl.tri.TriCache
#METHOD receive(org.jgroups.util.MessageBatch)
#AT EXIT
#IF TRUE
#     DO afterBatchDelivery($1);
#ENDRULE


