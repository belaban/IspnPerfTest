

## Byteman script to measure delivery latency from sending of a message until after the message has been
## serialized and put on the network


HELPER org.perf.DeliveryHelper

## Message is sent in JChannel.down(Message): the current time is added as a header to the message
RULE JChannelDown
CLASS org.jgroups.JChannel
METHOD down(org.jgroups.Message)
AT ENTRY
IF TRUE
    DO addSendTimeTo($1);
ENDRULE


## Takes the time before a message is passed on to the transport
RULE ComputeDownTime
CLASS org.jgroups.protocols.TP
METHOD send
AT ENTRY
IF TRUE
   DO computeDownTime($1);
ENDRULE

## Takes the time after a single message has been put on the network
RULE AfterTransportSendMessage
CLASS org.jgroups.protocols.BaseBundler
METHOD sendSingleMessage
AT EXIT
IF TRUE
   DO computeSendTime($1);
ENDRULE

## Takes the time after a message batch has been put on the network
RULE AfterTransportSendMessageBatch
CLASS org.jgroups.protocols.BaseBundler
METHOD sendMessageList
AT EXIT
IF TRUE
   DO computeSendTime($3);
ENDRULE


## Takes the time after a single message has been put on the network by the NoBundler
RULE NoBundlerTransport
CLASS org.jgroups.protocols.NoBundler
METHOD sendSingleMessage
AT EXIT
IF TRUE
   DO computeSendTime($1);
ENDRULE

RULE SenderSendsBundler
CLASS org.jgroups.protocols.SenderSendsBundler
METHOD send
AT EXIT
IF TRUE
   DO computeSendTime($1);
ENDRULE

RULE RingBufferBundler
CLASS org.jgroups.protocols.RingBufferBundler
METHOD sendBundledMessages
AFTER INVOKE org.jgroups.protocols.TP.doSend ALL
IF $msg != null
    DO computeSendTime($msg);
ENDRULE

RULE RingBufferBundlerLockless
CLASS org.jgroups.protocols.RingBufferBundlerLockless
METHOD sendBundledMessages
AFTER INVOKE org.jgroups.protocols.TP.doSend ALL
IF $msg != null
    DO computeSendTime($msg);
ENDRULE


RULE RingBufferBundlerLockless2
CLASS org.jgroups.protocols.RingBufferBundlerLockless2
METHOD sendBundledMessages
AFTER INVOKE org.jgroups.protocols.TP.doSend ALL
IF $msg != null
    DO computeSendTime($msg);
ENDRULE

## Rule for disruptor's bundled send (WIP)
#RULE DisruptorBundler
#CLASS org.perf.DisruptorBundler
#METHOD
#ENDRULE
